plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '1.5.10.RELEASE'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.node-gradle.node' version '2.2.4'
    id 'net.ltgt.apt' version '0.8'
    id 'net.researchgate.release' version '2.7.0'
}

group 'org.motechproject.mots'
version '0.1-SNAPSHOT'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/" }
    maven { url "http://jasperreports.sourceforge.net/maven2/" }
}

dependencies {
    implementation 'org.springframework:spring-context-support'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security.oauth:spring-security-oauth2'
    implementation 'org.springframework.security:spring-security-jwt'
    implementation 'mysql:mysql-connector-java'
    implementation 'org.hibernate:hibernate-java8'
    implementation 'com.zaxxer:HikariCP:3.4.2'
    implementation 'org.liquibase:liquibase-core:3.5.3'
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.poi:poi-ooxml:3.17'
    implementation 'net.sf.jasperreports:jasperreports:6.4.1'
    implementation 'net.sf.supercsv:super-csv:2.4.0'
    implementation 'com.google.guava:guava:24.1-jre'
    implementation 'org.mapstruct:mapstruct-jdk8:1.2.0.Final'

    compileOnly 'org.projectlombok:lombok:1.18.12'

    annotationProcessor 'org.mapstruct:mapstruct-processor:1.2.0.Final'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.+'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.5'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.5'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test:5.0.3.RELEASE'
}

processResources {
    filesMatching("**/application.yml") {
        expand(project.properties)
    }
}

node {
    version = '6.11.4'
    npmVersion = '3.10.10'
    download = true
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/.bin/webpack')
}

task webpackWatch(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/.bin/webpack')
    args = ['--watch']
}

task webpackProduction(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/.bin/webpack')
    args = ['-p']
}

jar.dependsOn 'webpackProduction'
bootRun.dependsOn 'webpack'

wrapper {
    gradleVersion = '6.0'
}

checkstyle {
    toolVersion '8.0'
    configFile = file("checkstyle.xml")
    maxWarnings = 0
}

pmd {
    toolVersion = '5.8.1'
    consoleOutput = true
    ignoreFailures = false
    ruleSetFiles = files("ruleset.xml")
    reportsDir = file("build/reports/pmd")
}

apply from: 'liquibase.gradle'
